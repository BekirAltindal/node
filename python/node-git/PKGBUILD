# Maintainer: Bekir Altindal <bekir.altindal@rwth-aachen.de>
# SPDX-FileCopyrightText: 2025 Bekir Altindal <bekir.altindal@rwth-aachen.de>
# SPDX-License-Identifier: Apache-2.0

pkgname=python-villas-node-git
_name=${pkgname#python-villas-}
pkgver=1.0.1
pkgrel=1
pkgdesc='Python support for the VILLASnode simulation-data gateway'
arch=(any)
url='https://github.com/VILLASframework/node'
license=('Apache-2.0')
depends=(
	'python>=3.10'
	'python-linuxfd>=1.5'
	'python-requests>=2.32.3'
	'python-protobuf>=6.31.1'
)
makedepends=(
	python-build
	python-installer
	python-setuptools
)
checkdepends=(
	'python-black>=25.1.0'
	'python-flake8>=7.2.0'
	'mypy>=1.15.0'
	'python-pytest>=8.3.5'
	'python-types-requests>=2.32.0.20250328'
	'python-types-protobuf>=5.29.1.20250403'
)
source=("${pkgname}-${pkgver}::git+https://github.com/VILLASframework/${_name}.git")
sha256sums=('SKIP')

build() {
	cd ${pkgname}-${pkgver}/python
	python -m build --wheel --no-isolation
}

check() {
	cd ${pkgname}-${pkgver}/python
	black --check --diff .
	flake8 --exclude=*_pb2.py,*_pb2.pyi .
	mypy .
	pytest -v .
}

package() {
	cd ${pkgname}-${pkgver}/python
	python -m installer --destdir="$pkgdir" dist/*.whl
}
